plugins {
    id "java"
    id "idea"
    id "eclipse"
    id "maven-publish"
    id "com.gradleup.shadow" version "9.3.0"
    id "net.neoforged.licenser" version "0.7.5"
}

group "dev.architectury"

def isSnapshot = System.getenv("PR_NUM") != null
def runNumber = (System.getenv("GITHUB_RUN_NUMBER") == null ? "9999" : System.getenv("GITHUB_RUN_NUMBER"))

if (!isSnapshot) {
    version = base_version + "." + runNumber
} else {
    version = base_version + "-PR." + System.getenv("PR_NUM") + "." + runNumber
}

logger.lifecycle(":building architectury transformer v${version}")

/*java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8)
    }
}*/

repositories {
    maven { url "https://maven.architectury.dev/" }
    maven { url "https://maven.fabricmc.net/" }
    mavenCentral()
}

sourceSets {
    agent
    runtime {
        compileClasspath += sourceSets.main.runtimeClasspath
        runtimeClasspath += sourceSets.main.runtimeClasspath
        compileClasspath += sourceSets.agent.runtimeClasspath
        runtimeClasspath += sourceSets.agent.runtimeClasspath
    }
}

license {
    header = rootProject.file("HEADER")

    ext {
        name = "architectury"
        year = "2020, 2021, 2022"
    }

    include "**/*.java"
}

dependencies {
    implementation "net.fabricmc:mapping-io:0.7.1"
    implementation "net.fabricmc:tiny-remapper:0.11.2"
    implementation "org.ow2.asm:asm:9.9.1"
    implementation "org.ow2.asm:asm-commons:9.9.1"
    implementation "org.ow2.asm:asm-tree:9.9.1"
    implementation "org.ow2.asm:asm-util:9.9.1"
    implementation "com.google.guava:guava:33.5.0-jre"
    implementation "com.google.code.gson:gson:2.13.2"
    implementation "dev.architectury:refmap-remapper:1.0.5"
    compileOnly "org.jetbrains:annotations:26.0.2"
}

tasks.register('sourcesJar', Jar) {
    dependsOn classes
    archiveClassifier.set "sources"
    from sourceSets.main.allSource
}

tasks.register('agentJar', Jar) {
    archiveClassifier.set "agent"
    manifest {
        attributes(
                "Premain-Class": "dev.architectury.transformer.agent.TransformerAgent",
                "Agent-Class": "dev.architectury.transformer.agent.TransformerAgent",
                "Can-Redefine-Classes": true,
                "Can-Retransform-Classes": true,
                "Automatic-Module-Name": "dev.architectury.transformer.agent"
        )
    }
    from sourceSets.agent.output
}

tasks.register('agentSourcesJar', Jar) {
    dependsOn classes
    archiveClassifier.set "agent-sources"
    from sourceSets.agent.allSource
}

tasks.register('runtimeJar', Jar) {
    dependsOn shadowJar
    archiveClassifier.set "runtime"
    manifest {
        attributes(
                "Main-Class": "dev.architectury.transformer.TransformerRuntime",
                "Implementation-Version": project.version,
                "Automatic-Module-Name": "dev.architectury.transformer.runtime"
        )
    }
    from {
        sourceSets.runtime.output + zipTree(shadowJar.archiveFile)
    }
}

tasks.register('runtimeSourcesJar', Jar) {
    dependsOn classes
    archiveClassifier.set "runtime-sources"
    from sourceSets.runtime.allSource
}

jar {
    archiveClassifier.set "raw"
}

shadowJar {
    relocate "net.fabricmc", "dev.architectury.transformer.shadowed.impl.net.fabricmc"
    relocate "org.objectweb", "dev.architectury.transformer.shadowed.impl.org.objectweb"
    relocate "org.checkerframework", "dev.architectury.transformer.shadowed.impl.org.checkerframework"
    relocate "org.zeroturnaround", "dev.architectury.transformer.shadowed.impl.org.zeroturnaround"
    relocate "org.slf4j", "dev.architectury.transformer.shadowed.impl.org.slf4j"
    relocate "com.google", "dev.architectury.transformer.shadowed.impl.com.google"
    relocate "javax.annotation", "dev.architectury.transformer.shadowed.impl.javax.annotation"
    relocate "dev.architectury.tinyremapper", "dev.architectury.transformer.shadowed.impl.dev.architectury.tinyremapper"
    relocate "dev.architectury.refmapremapper", "dev.architectury.transformer.shadowed.impl.dev.architectury.refmapremapper"
    configurations = [project.configurations.runtimeClasspath]
    archiveClassifier.set "shadow"
    exclude "module-info.class"
}

tasks.register('mainJar', Jar) {
    dependsOn shadowJar
    from zipTree(shadowJar.archiveFile)
}

build.dependsOn mainJar
build.dependsOn runtimeJar

publishing {
    publications {
        main(MavenPublication) {
            artifact(mainJar)
            artifact(sourcesJar)
        }
        runtime(MavenPublication) {
            artifact(runtimeJar)
            artifact(runtimeSourcesJar)
        }
        agent(MavenPublication) {
            artifact(agentJar)
            artifact(agentSourcesJar)
        }
    }

    repositories {
        if (System.getenv("MAVEN_PASS") != null) {
            maven {
                url = "https://deploy.shedaniel.me/"
                credentials {
                    username = "shedaniel"
                    password = System.getenv("MAVEN_PASS")
                }
            }
        }
        mavenLocal()
    }
}